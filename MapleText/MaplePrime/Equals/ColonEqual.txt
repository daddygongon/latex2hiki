等号(ColonEqual)
解説

等号は，数学でいろいろな意味を持つことを中学校で学ぶ．等号それぞれの状況による操作の違いを人間は適当に判断できるが，プログラムであるMapleではそれぞれ違った
記号や操作として用意され，人間がMapleに指示する必要がある．
変数への代入:= (colonequal)
変数に値を代入する時には:= (colonequal)を使う．例えば，
a=3, b=2のとき，a+bはいくらか？
という問題を，Mapleで解かす時には，
aに3, bに2を代入したとき，a+bはいくらか？
と読み直し，
> a:=3; b:=2; a+b;
                                      3
                                      2
                                      5
式の定義も同様．以下はa*x...=c*x^2...という式をeq1として定義している．
> eq1:=a*x+b=c*x^2+d*x+e;
                                        2          
                           3 x + 2 = c x  + d x + e
変数の初期化(restart)
一度何かを代入した変数を何も入れていない状態に戻す操作を変数の初期化という．すべての変数を一度に初期化するには，
> restart;
とする．ひとつの変数だけを初期化するには，シングルクォート’でくくる．
> a:='a';
                                      a
方程式の解(solve)
3x=2を満たすxをもとめよ．
という問題は，
> solve(3*x=2,x);
                                      2
                                      -
                                      3
連立方程式は以下のとおり．
> solve({x+y=1,x-y=2},{x,y});
                               /    3      -1\ 
                              { x = -, y = -- }
                               \    2      2 / 
ただし，solveだけでは，x,yに値は代入されない．
> sol1:=solve({x+y=1,x-y=2},{x,y});
> assign(sol1);
                               /    3      -1\ 
                              { x = -, y = -- }
                               \    2      2 / 
とする必要がある．確認してみると
> x,y;
                                    3  -1
                                    -, --
                                    2  2 
となり，値が代入されていることがわかる．
方程式の数値解(fsolve)
解析的に解けない場合は，数値的に解を求めるfsolveを使う．
> x:='x';
> fsolve(log(x)-exp(-x),x);
                                      x
                                 1.309799586
式の変形
式の変形にも等号が使われる．例えば，
(x-2)^2=x^2-4x+4
というのが等号で結ばれている．式の変形とは，変数xがどんな値であっても成り立つ恒等的な変形である．
この式変形も，問題としては，
(x-2)^2を展開(expand)せよ
と与えられるので，そのままMapleコマンドに読み替えて
> expand((x-2)^2);
                                  2          
                                 x  - 4 x + 4
とすればよい．因数分解(factor)や微分(diff)・積分(int)も同様に等号で結ばれるが，Mapleには操作を指示する必要がある．詳しくは他の単元で．
課題
1. a=3, b=4としてa,bの四則演算をおこなえ．また，べき乗a^bを求めよ．
2. eq1=3*x+4=2*x-2, a=2とした場合のeq1/a,eq1+aを試し，両辺を観察せよ．
3. 3点(1,2),(-3,4),(-1,1)を通る２次方程式を求めよ．
4.方程式sin(x+1)-x^2=0の２つの解をfsolveのヘルプを参照して求めよ．
5.
関数y=exp(x)の逆関数を求めよ．2つの関数およびy=xを同時にx=-5..5,y=-5..5でplotし，y=xに対して２つの関数が対称であることを確認せ
よ．
解答例
1.
> a:=3:
> b:=4:
> a+b;a-b;a*b;a/b;a^b;
省略
                                      7
                                      -1
                                      12
                                      3
                                      -
                                      4
                                      81
2.
> eq1:=3*x+4=2*x-2;
> a:=2;
> eq1/a;
> eq1+2;
                              3 x + 4 = 2 x - 2
                                      2
                               3              
                               - x + 2 = x - 1
                               2              
                                3 x + 6 = 2 x
3.
まず2次関数を定義する．
> restart;
> f:=x->a*x^2+b*x+c;
        2          
x -> a x  + b x + c
(1,2)と通ることから，f(1)=2が成立．これをeq1として保存．
> eq1:=f(1)=2;
                                a + b + c = 2
他の点も同様に
> eq2:=f(-3)=4;
> eq3:=f(-1)=1;
                              9 a - 3 b + c = 4
                                a - b + c = 1
この3個の連立方程式から，a,b,cを求めれば解となる．
> solve({eq1,eq2,eq3},{a,b,c});
                            /    1      1       \ 
                           { a = -, b = -, c = 1 }
                            \    2      2       / 
4.
まず，2つの関数とみなしてプロット．
> plot([sin(x+1),x^2],x=-1..1);

解が２つあることに注意．与えられた関数値が０となる方程式として定義し，これをsolveでとく．
> eq1:=sin(x+1)-x^2=0;
> solve(eq1,x);
                                           2    
                             sin(x + 1) - x  = 0
                               /                        2\
                    -1 + RootOf\-sin(_Z) + 1 - 2 _Z + _Z /
これでは解を求めてくれないので，fsolveで数値解を求める．
> fsolve(eq1,x);
                                 0.9615690350
これではxの負にあるもう一つの解がでない．これを解決するには，fsolveでxに初期値を入れて実行する．
> fsolve(eq1,x=-1..0);
                                -0.6137631294
5.
先ず，与関数をf(x)として定義しておく．
> f:=x->exp(x);
x -> exp(x)
y=f(x)として，xについて解いてみる．
> eq1:=y=f(x);
> solve(eq1,x);
                                  y = exp(x)
                                    ln(y)
うまくyの関数として解けている．これをea2と定義し直して，逆関数として定義する．
> eq2:=solve(eq1,x);
> invf:=unapply(eq2,y);
                                    ln(y)
y -> ln(y)
同時にプロットしてみる．
> plot([f(x),invf(x),x],x=-5..5,y=-5..5);

y=xを軸として，2つの関数が対称であることが確認できる．
> 
