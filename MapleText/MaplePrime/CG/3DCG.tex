\subsection{listplot, pointplot}
リスト構造にある離散的なデータはlistplotで表示してくれる．listplotは受け取ったlistの要素をy値に，1から始まる添字をx値にして，デフォルトで
は線でグラフを書く．
\begin{MapleInput}
> T:=[seq(exp(-i),i=0..5)]; 
> listplot(T);
\end{MapleInput}
\begin{MapleOutput}
T\, := \,[1,\exp(-1),\exp(-2),\exp(-3),\exp(-4),\exp(-5)]
\end{MapleOutput}
\MaplePlot{50mm}{./figures/MapleCGplot2d1.eps}

以下のようにoptionをつけるとpointで描く．
\begin{MapleInput}
> listplot(T,style=point):
\end{MapleInput}

それぞれの値の横軸xが1,2,3,..では不都合なときには，2次元のlistlist構造を用意し，[x[i],y[i]]を入れてpointplot関数で表示する
．
\begin{MapleInput}
> T:=[seq([i/2,exp(-i/2)],i=0..6)]; 
> pointplot(T,symbol=circle,symbolsize=20);
\end{MapleInput}
\begin{MapleOutputGather}
T\, := \,[[0,1],[1/2,\exp(-1/2)],[1,\exp(-1)],[3/2,\exp(-3/2)], \notag \\
[2,\exp(-2)],[5/2,\exp(-5/2)],[3,\exp(-3)]]
\end{MapleOutputGather}
\MaplePlot{50mm}{./figures/MapleCGplot2d2.eps}


listplotのように線でつなぎたい時には，以下のようにoptionをつける．
\begin{MapleInput}
> pointplot(T,connect=true):
\end{MapleInput}

\subsection{写像の表示}
ある行列によって点を移動させる写像の様子を示すスクリプトを通して，plottoolsが提供するdisk, arrowの使い方を示す．先ず描画に必要なライブラリーパッケージ(plotsおよびplottools)をwithで読み込んでおく．
\begin{MapleInput}
> restart; with(plots):with(plottools):
\end{MapleInput}
\begin{MapleOutput}
\end{MapleOutput}
行列$A= \left[ \begin {array}{cc} 1&2\\ 2&1\end {array} \right]$
によって点$a_0$(1, 2)が$a_1$(5, 4)に移動するとする(LinearAlgebra参照）．
\begin{MapleInput}
> with(LinearAlgebra): A:=Matrix([[1,2],[2,1]]): a0:=Vector([1,2]): a1:=A.a0;
\end{MapleInput}
\begin{MapleOutput}
{\it a1}\, := \, \left[ \begin {array}{c} 5\\ 4\end {array} \right]
\end{MapleOutput}
ベクトルが位置座標を意味するようにlistへ変換(convert)する．
\begin{MapleInput}
> p0:=convert(a0,list):p1:=convert(a1,list):
\end{MapleInput}
位置p0に円(disk)を半径0.2,赤色で描く．同じように位置p1に半径0.2，青色でdiskを描く．
\begin{MapleInput}
> point1:=[disk(p0,0.2,color=red),disk(p1,0.2,color=blue)]:
\end{MapleInput}
もう一つ，p0からp1に向かう矢印(arrow)を適当な大きさで描く．後ろの数字をいじると線の幅や矢印の大きさが変わる．
\begin{MapleInput}
> line1:=arrow(p0,p1,0.05,0.3,0.1):
\end{MapleInput}
これらをまとめて表示(display)．このとき，表示範囲を0..6,0..6とする．
\begin{MapleInput}
> display(point1,line1,view=[0..6,0..6],gridlines=true);
\end{MapleInput}
\MaplePlot{50mm}{./figures/MapleCGplot2d3.eps}
$a_0$(1, 2)の赤点が，$a_1$(5, 4)の青点へ移動していることを示している．

\subsection{回転写像}
次に原点周りでの回転の様子を示す．回転の行列．
\begin{MapleInput}
> Matrix([[cos(theta),sin(theta)],[-sin(theta),cos(theta)]]);
\end{MapleInput}
\begin{MapleOutput}
\left[ \begin {array}{cc} \cos \left( \theta \right) &\sin \left( \theta \right) \\ -\sin \left( \theta \right) &\cos \left( \theta \right) \end {array} \right]
\end{MapleOutput}
これを関数のように定義している．
\begin{MapleInput}
> A:=t->Matrix([[cos(t),sin(t)],[-sin(t),cos(t)]]);
\end{MapleInput}
\begin{MapleOutput}
A\, := \,t\mapsto  \left[ \begin {array}{cc} \cos \left( t \right) &\sin \left( t \right) \\ -\sin \left( t \right) &\cos \left( t \right) \end {array} \right]
\end{MapleOutput}
tに回転角(Pi/3)を入れている．
\begin{MapleInput}
> a0:=Vector([3,0]);
> a1:=A(Pi/3).a0;
\end{MapleInput}
\begin{MapleOutputGather}
{\it a0}\, := \, \left[ \begin {array}{c} 3\\ 0\end {array} \right] \notag \\
{\it a1}\, := \, \left[ \begin {array}{c} 3/2\\ -3/2\,\sqrt {3}\end {array} \right] \notag
\end{MapleOutputGather}
表示の仕方は，前節と同じ．
\begin{MapleInput}
> p0:=convert(a0,list):p1:=convert(a1,list):
> point1:=[disk(p0,0.2,color=red),disk(p1,0.2,color=blue)]:
> line1:=arrow(p0,p1,0.05,0.3,0.1):
> display(point1,line1,view=[-4..4,-4..4],gridlines=true);
\end{MapleInput}
\MaplePlot{50mm}{./figures/MapleCGplot2d4.eps}

\subsection{平行投影図の作成}
もう少し複雑な対象物として，ここでは立方体の表示を考える．まず３次元座標を打ち込む．
\begin{MapleInput}
> restart; with(plots): with(plottools): 
  p:=[[0,0,0],[1,0,0],[1,1,0],[0,1,0],
  [0,0,1],[1,0,1],[1,1,1],[0,1,1]]:
\end{MapleInput}
\begin{MapleOutput}
\end{MapleOutput}
次にこれをpointplot3dで簡便に表示．
\begin{MapleInput}
> points:= { seq(p[i],i=1..8) }:
> pointplot3d(points,symbol=circle,symbolsize=40,color=black);
\end{MapleInput}
\MaplePlot{50mm}{./figures/MapleCGplot3d5.eps}
もうすこし見やすいように頂点を結んでおく．たとえば，p[1]とp[2]との間を線で結ぶには，

\begin{MapleInput}
> line(p[1],p[2]);
\end{MapleInput}
とする．それをseqで複数の点間に対して施す．

\begin{MapleInput}
> ll:=[[1,2],[2,3],[3,4],[4,1],[1,5],[2,6],[3,7],[4,8],
> [5,6],[6,7],[7,8],[8,5]]:
> lines:=[seq(line(p[ll[i][1]],p[ll[i][2]]),i=1..nops(ll))]:
> display(lines,scaling=constrained,color=black);
\end{MapleInput}
\MaplePlot{50mm}{./figures/MapleCGplot3d6.eps}

\begin{MapleInput}
> l3:=display(lines,scaling=constrained,color=black):
> p3:=pointplot3d(p,symbol=circle,symbolsize=40,color=black):
> display([p3,l3],scaling=constrained,color=black);
\end{MapleInput}
\MaplePlot{50mm}{./figures/MapleCGplot3d7.eps}


画像をつまんでぐるぐる回してみよ．Mapleではこんな操作は簡単にできるが，よく見ればわかるように，この3次元表示では透視図ではなく，平行投影図といわれるものを書いている．

\subsection{透視図}
透視図のもっとも簡単な変換法は
\begin{MapleInput}
> proj2d:=proc(x,z) 
    local x1,y1; 
    x1:=x[1]*z/(z-x[3]); 
    y1:=x[2]*z/(z-x[3]);
    return [x1,y1]; 
  end proc:
\end{MapleInput}
\begin{MapleOutput}
\end{MapleOutput}
zに視点の距離を入れて，xで座標を受け取って変換した結果を[x1,y1]として返している．この関数を前の表示と組み合わせれば透視図の描画ができる．
\begin{MapleInput}
> z_p:=-8:
  lines:=[seq(line(proj2d(p[ll[i][1]],z_p), proj2d(p[ll[i][2]],z_p)), i=1..nops(ll))]:
  display(lines);
\end{MapleInput}
\MaplePlot{50mm}{./figures/MapleCGplot2d8.eps}

\subsection{Mapleの描画関数の覚書}
mapleにはいくつかの描画レベルに合わせた関数が用意されている．どのような目的にどの関数（パッケージ）を使うかの選択指針として，それぞれがどのような意図で作ら
れ，それらの依存関係は以下の通り．
\begin{description}
\item[描画の下位関数]
plot[structure]にあるPLOT,PLOT3Dデータ構造が一番下でCURVES,POINTS,POLYGONS,TEXTデータを元に絵を描く．
\item[plottoolsパッケージ]
PLOTよりもう少し上位で，グラフィックスの基本形状を生成してくれる関数群．arc, arrow, circle, curve, line, point,sphereなどの関数があり，PLOT構造を吐く．表示にはplots[display]を使う．
\item[plotsパッケージ]
簡単にグラフを書くための道具．たとえばlistplotは，listデータを簡易に表示する事を目的としている．
\end{description}
