数式変形実践課題（大学入試センター試験の解法を通して）
今まで出てきたコマンドを使えば，典型的なセンター試験の問題を解くのも容易である．以下の例題を参照して課題を解いてみよ．使うコマンドは，unapply,
solve, diff, expand(展開),
factor(因数分解)とsubs(一時的代入)である．expand等の数式変形によく使うコマンドは次節以降で詳しく解説している．subsは以下を参考にせよ．
解説
一時的代入(subs)
代入(:=)に対して，そこだけでの代入をsubsで行う．
> subs(a=1,a+2);
                                      3
典型的な使い方は，solveで求めた解などを式として代入しておいて，それをsubsで一時的に当てはめる．
> eq1:=a=solve(a+b=0,a); subs(eq1,a+2);
                                    a = -b
                                    -b + 2
例題
１．
a,bを定数とし,a <> 0とする．2次関数
y = ax^2-bx-a+b　(1)
のグラフが点 (-2, 6) を通るとする.
このとき
b = -a+[ア]
であり,グラフの頂点の座標を a を用いて表すと
(-a+[イ])/([ウ]*a), -([エ]*a+[-オ])^2/([カ]*a)
である (2008 年度大学入試センター試験数学 I より抜粋).
解答例
まず，与えられた２次関数をf(x)*`で定義する．`
> restart; f:=unapply(a*x^2-b*x-a+b,x);
        2              
x -> a x  - b x - a + b
与えられた点の座標を関数に入れる．
> eq1:=f(-2)=6;
                                3 a + 3 b = 6
これをbについて解く．
> eq2:=b=solve(eq1,b);
                                  b = 2 - a
次は，頂点の座標で傾きが0になることを用いて解いていく．
> solve(diff(f(x),x)=0,x);
                                      b 
                                     ---
                                     2 a
bの値はeq2で求まっているので，それを代入(subs)する．
> subs(eq2,solve(diff(f(x),x)=0,x));
                                    2 - a
                                    -----
                                     2 a 
これをx0としてeq3で定義しておく．
> eq3:=x0=subs(eq2,solve(diff(f(x),x)=0,x));
                                       2 - a
                                  x0 = -----
                                        2 a 
頂点のy座標は，f(x0)で求まる
> f(x0);
                                 2               
                             a x0  - b x0 - a + b
eq2,eq3で求まっているx0,bを代入する．
> eq4:=subs({eq2,eq3},f(x0));
                                      2          
                               (2 - a)           
                             - -------- - 2 a + 2
                                 4 a             
これを因数分解(factor)する．
> factor(subs({eq2,eq3},f(x0)));
                                            2
                                   (3 a - 2) 
                                 - ----------
                                      4 a    
課題
１．
P = (x(x+3))(2*x-3)とする．また，aを定数とする．
x = a+1のときのPの値は
2*a^3+[ア]*a^2+[イ]*a+[-ウ]
である．
x = a+1のときのPの値と，x = aのときのPの値が等しいとする．このとき，aは
3*a^2+[エ]*a+[-オ] = 0
を満たす．したがって
a = ([カキ]+`&+-`(sqrt([クケ])))/[コ]
である.
> arestart:
> P:=unapply(x*(x+3)*(2*x-3),x);
x -> x (x + 3) (2 x - 3)
> expand(P(a+1));
                               3      2          
                            2 a  + 9 a  + 3 a - 4
> expand(P(a));
                                 3      2      
                              2 a  + 3 a  - 9 a
> eq1:=(expand(P(a+1))-expand(P(a)))/2;
                                   2          
                                3 a  + 6 a - 2
> sol1:=solve(eq1=0,a);
                             1   (1/2)       1   (1/2)
                        -1 + - 15     , -1 - - 15     
                             3               3        
２．
（例題１．に引き続いて，）
さらに，２次関数(1)のグラフの頂点のy座標が-2であるとする．このとき，aは
[キ]*a^2-[クケ]*a+[コ] = 0
を満たす．これより，aの値は
a = [サ], [シ]/[ス]
である．
以下，a = [シ]/[ス]であるとする．
このとき，２次関数(1)のグラフの頂点のx座標は[セ]であり，(1)のグラフとx軸の２交点のx座標は[ソ],[タ]である．
また，関数(1)は0 <= x and x <= 9において
x = [チ]のとき，最小値[ツテ]をとり， x = [ト]のとき，最大値[ナニ]/[ヌ]をとる．
(2008 年度大学入試センター試験数学 I より抜粋).
> eq4;
                                      2          
                               (2 - a)           
                             - -------- - 2 a + 2
                                 4 a             
> eq5:=expand((eq4+2)*(-4)*a);
                                             2
                               4 - 20 a + 9 a 
> solve(eq5=0,a);
                                        2
                                     2, -
                                        9
> subs(a=2/9,eq3);
                                    x0 = 4
> eq6:=subs({a=2/9,b=2-2/9},f(x));
                               2  2   16     14
                               - x  - -- x + --
                               9      9      9 
> solve(eq6=0,x);
                                     7, 1
> plot(subs({a=2/9,b=2-2/9},f(x)),x=0..9);

> subs({a=2/9,b=2-2/9},f(4)); subs({a=2/9,b=2-2/9},f(9));
                                      -2
                                      32
                                      --
                                      9 
> 
