式の変形(I, simplify)
Copyright @2010 by Shigeto R. Nishitani


数式の変形は，手で直すほうが圧倒的に早くきれいになる場合が多い．しかし，テイラー展開や，複雑な積分公式，三角関数とexp関数の変換などの手間がかかるところを，M
apleは間違いなく変形してくれる．ここで示すコマンドを全て覚える必要は全くない．というか忘れるもの．ここでは，できるだけコンパクトにまとめて，悩んだときに参照
できるようにする．初めての人は，ざっと眺めた後，鉄則からじっくりフォローせよ．
解説
コマンドの分類
まず数式処理でよく使うコマンドをいくつかの範疇に分類してまとめておく．このほかにも前に示した，solve(解), diff(微分),
int(積分)，series(級数展開)等は頻繁に数式の導出・変形に登場する．
式の変形
式の分割抽出
代入，置換，仮定
省略操作，その他
simplify:簡単化
expand:展開
factor:因数分解
normal:約分・通分
combine:公式でまとめる
collect:次数でまとめる
sort:昇べき，降べき
convert:形式の変換

式の分割抽出
lhs, rhs:左辺，右辺
numer, denom:分子，分母
coeff:係数
nops, op

代入，置換，仮定
subs:一時的代入
asume:仮定
assuming:一時的仮定
assign:値の確定
about:仮定の中身
anames('user'):使用変数名
restart,a:='a':初期化

省略操作，その他
||:連結作用素
seq:for-loopの簡易表記
map:関数の要素への適用
add,mul:単純な和，積
sum,product:数式に対応した和，積
limit:極限

式の変形に関連したコマンド
simplify(exp1):簡単化> simplify(exp1,副関係式):
> simplify(3*x+4*x+2*y);
                                  7 x + 2 y
> exp1:=3*sin(x)^3-sin(x)*cos(x)^2; simplify(exp1);
                                  3                2
                          3 sin(x)  - sin(x) cos(x) 
                            /        2    \       
                           -\4 cos(x)  - 3/ sin(x)
> simplify(exp1,{cos(x)^2=1-sin(x)^2});
                                      3         
                              4 sin(x)  - sin(x)
オプションとしてsizeを指定するとより簡単になる場合がある．
> simplify(exp1,size):
expand(exp1):展開
> expand((x+y)^2);
                                2            2
                               x  + 2 x y + y 
factor(exp1):因数分解
> factor(4*x^2-6*x*y+2*y^2);
                             2 (2 x - y) (x - y)
normal(exp1):約分・通分
> normal((x+y)/(x^2-3*x*y-4*y^2));
                                      1   
                                   -------
                                   x - 4 y
> normal(1/x+1/y);
                                    y + x
                                    -----
                                     x y 
collect(exp1,exp2):項を変数でまとめる
> collect(4*a*x^2-3*y^2/x+6*b*x*y+3*c*y+2*y^2,y);
                   /  3    \  2                          2
                   |- - + 2| y  + (6 b x + 3 c) y + 4 a x 
                   \  x    /                              
combine(exp1):項を公式でまとめる
> combine(sin(x)^2+3*cos(x)^2);
                                 2 + cos(2 x)
sort(exp1):昇べき，降べき
> sort(exp1,[x,y]); > sort(exp1, [x],
opts);opts=tdeg(総次数順),plex(辞書式順),ascending(昇順), descending(降順)
> exp1:=x^3+4*x-3*x^2+1: sort(exp1);
                              3      2          
                             x  - 3 x  + 4 x + 1
> sort(exp1,[x],ascending);
                                          2    3
                             1 + 4 x - 3 x  + x 
> exp2:=x^3-a*x*y+4*x^2+y^2: sort(exp2);
                                     3      2    2
                           -a x y + x  + 4 x  + y 
> sort(exp2,[x]);
                             3      2            2
                            x  + 4 x  - a y x + y 
> sort(exp2,[a,y]); sort(exp2,[a],plex);
                                     2    3      2
                           -x a y + y  + x  + 4 x 
                                     2    3      2
                           -x y a + y  + x  + 4 x 
演習
1. 以下の式を簡単化せよ． i) x^100-1 ii) x^2-y^2+2*x+1 iii) (a+b+c)^3-(a^3+b^3+c^3)
> factor(x^100-1);
        /     4    3    2    \ /     20    15    10    5\         /         2
(x - 1) \1 + x  + x  + x  + x/ \1 + x   + x   + x   + x / (1 + x) \1 - x + x 

      3    4\ /     5    10    15    20\ /     2\ / 8    6    4    2    \ / 40
   - x  + x / \1 - x  + x   - x   + x  / \1 + x / \x  - x  + x  - x  + 1/ \x  

      30    20    10    \
   - x   + x   - x   + 1/
> factor( x^2-y^2+2*x+1);
                           (x + 1 + y) (x + 1 - y)
> factor((a+b+c)^3-(a^3+b^3+c^3));
                          3 (b + c) (c + a) (a + b)
> 
