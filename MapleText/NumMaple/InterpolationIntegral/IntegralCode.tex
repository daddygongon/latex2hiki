次の積分を例に，Mapleのコードを示す．
\begin{equation*}
\int_0^1 \frac{4}{1+x^2} \, dx
\end{equation*}
先ずは問題が与えられたらできるだけMapleで解いてしまう．答えをあらかじめ知っておくと間違いを見つけるのが容易．プロットしてみる．
\begin{MapleInput}
> restart; 
  f1:=x->4/(1+x^2); 
  plot(f1(x),x=0..5);
\end{MapleInput}
\begin{MapleOutput}
{\it f1}\, := \,x\mapsto \frac{4}{1+{x}^{2}}
\end{MapleOutput}
\MaplePlot{50mm}{./figures/C7_InterpolationIntegralplot2d3.eps}
Mapleで解いてみる．
\begin{MapleInput}
>int(f1(x),x=0..1);
\end{MapleInput}
\begin{MapleOutput}
\pi
\end{MapleOutput}
えっと思うかも知れないが，
\begin{MapleInput}
>int(1/(1+x^2),x);
\end{MapleInput}
\begin{MapleOutput}
arctan(x)
\end{MapleOutput}
となるので，納得できるでしょう．

具体的にMapleでコードを示す．先ずは初期設定．
\begin{MapleInput}
>N:=8: x0:=0: xn:=1: Digits:=20:
\end{MapleInput}

\paragraph{Midpoint rule(中点法)}　
\begin{MapleInput}
> h:=(xn-x0)/N: S:=0: 
  for i from 0 to N-1 do 
    xi:=x0+(i+1/2)*h; 
    dS:=h*f1(xi);
    S:=S+dS; 
  end do: 
  evalf(S);
\end{MapleInput}
\begin{MapleOutput}
3.1428947295916887799
\end{MapleOutput}

\paragraph{Trapezoidal rule(台形公式)}　
\begin{MapleInput}
> h:=(xn-x0)/N: S:=f1(x0)/2: 
  for i from 1 to N-1 do 
    xi:=x0+i*h; 
    dS:=f1(xi);
    S:=S+dS; 
  end do: 
  S:=S+f1(xn)/2: 
  evalf(h*S);
\end{MapleInput}
\begin{MapleOutput}
3.1389884944910890093
\end{MapleOutput}

\paragraph{Simpson's rule(シンプソンの公式)}　
\begin{MapleInput}
> M:=N/2: h:=(xn-x0)/(2*M): Seven:=0: Sodd:=0: 
  for i from 1 to 2*M-1 by 2 do
    xi:=x0+i*h; 
    Sodd:=Sodd+f1(xi); 
  end do: 
  for i from 2 to 2*M-1 by 2 do
    xi:=x0+i*h; 
    Seven:=Seven+f1(xi); 
  end do:
  evalf(h*(f1(x0)+4*Sodd+2*Seven+f1(xn))/3);
\end{MapleInput}
\begin{MapleOutput}
3.1415925024587069144
\end{MapleOutput}

