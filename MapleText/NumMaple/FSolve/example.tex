代数方程式に関する次の課題に答えよ．(2004年度期末試験)
\begin{enumerate}
\item $\exp(-x) = x^2$を二分法およびニュートン法で解け.
\item $n$回目の値$x_n$と小数点以下10桁まで求めた値$x_f=0.7034674225$との差$\Delta x_n$の絶対値(abs)のlogを$n$の関数としてプロットし，その収束性を比較せよ．また，その傾きの違いを両解法の原理から説明せよ.
\end{enumerate}

\subsubsection{解答例}
計算精度を40桁にしておく．funcで関数を定義．
\begin{MapleInput}
> restart; Digits:=40: func:=unapply(exp(-x)-x^2,x);
\end{MapleInput}
\begin{MapleOutput}
func\, := \,x\mapsto {e}^{-x}-{x}^{2}
\end{MapleOutput}
先ずは，関数をplotして概形を確認．
\begin{MapleInput}
> plot(func(x),x=-5..5,y=-10..10);
\end{MapleInput}
\MaplePlot{50mm}{./figures/fsolveExampleplot2d1.eps}
Mapleの組み込みコマンドで正解を確認しておく．
\begin{MapleInput}
> x0:=fsolve(func(x)=0,x);
\end{MapleInput}
\begin{MapleOutput}
0.7034674224983916520498186018599021303429
\end{MapleOutput}
テキストからプログラムをコピーして走らせてみる．環境によっては，printf分の中の"\verb|\|"が文字化けしているので修正．
\begin{MapleInput}
> x1:=0: x2:=0.8: res1:=[]:
  f1:=func(x1): f2:=func(x2):
  for i from 1 to 20 do
    x:=(x1+x2)/2;
    f:=func(x);
    if f*f1>=0.0 then
      x1:=x; f1:=f;
    else
      x2:=x; f2:=f;
    end if;
    printf("%20.15f, %20.15f\n",x,f); 
    res1:=[op(res1),[i,abs(x-x0)]]:
  end do:
\end{MapleInput}
\begin{MapleError}
0.400000000000000, 0.510320046035639 
0.600000000000000, 0.188811636094026
0.700000000000000, 0.006585303791410 
0.750000000000000, -0.090133447258985
0.725000000000000, -0.041300431044638 

中略

0.703468322753906, -0.000001712107681 
0.703467559814453, -0.000000261147873
\end{MapleError}
プロットのためにリストを作成する．Mapleでこれを実行するイディオムは以下の通り．
\begin{MapleInput}
> res1:=[];
  for i from 1 to 3 do
    res1:=[op(res1),[i]];
  end do;
\end{MapleInput}
これを先のコードに組み込んでおいて得られた結果をlogplot(片対数プロット)する．
\begin{MapleInput}
> with(plots): 
> logplot(res1); #res:結果は後に示す
\end{MapleInput}

同様にNewton法での結果をres2に入れる．
\begin{MapleInput}
> dfunc:=unapply(diff(func(z),z),z);
\end{MapleInput}
\begin{MapleOutput}
{\it dfunc}\, := \,z\mapsto -{{e}^{-z}}-2\,z
\end{MapleOutput}
\begin{MapleInput}
> x:=1.0: f:=func(x): 
  printf("%15.10f, %+24.25f\n",x,f); 
  res2:=[[1,abs(x-x0)]]:
  for i from 2 to 5 do
    x:=x-f/dfunc(x);
    f:=func(x);
    printf("%15.10f, %+24.25f\n",x,f); 
    res2:=[op(res2),[i,abs(x-x0)]]:
  end do:
\end{MapleInput}
\begin{MapleError}
1.0000000000, -0.6321205588285576784044762 
0.7330436052, -0.0569084480040254074684576 
0.7038077863, -0.0006473915387465014761973
0.7034674683, -0.0000000871660305624231097
0.7034674225, -0.0000000000000015809178420
\end{MapleError}

res1, res2を片対数プロットして同時に表示．
\begin{MapleInput}
> l1:=logplot(res1);
> l2:=logplot(res2);
> display(l1,l2);
\end{MapleInput}
\MaplePlot{50mm}{./figures/fsolveExampleplot2d2.eps}
2分法で求めた解は，Newton法で求めた解よりもゆっくりと精密解へ収束している．これは，二分法が原理的に計算回数について一次収束なのに対して，Newton法は2次収束であるためである．解の差($\delta$)だけでなく，関数値$f(x),\epsilon$をとっても同様の振る舞いを示す．
