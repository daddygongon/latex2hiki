線形代数計算のためにサブパッケージとしてLinearAlgebraを呼びだしておく．
\begin{MapleInput}
> restart; 
  with(plots): 
  with(LinearAlgebra):
\end{MapleInput}


データを読み込む．
\begin{MapleInput}
> ndata:=8: 
  f1:=t->subs({a1=1,a2=10,a3=1,a4=4},a1+a2/(a3+(t-a4)^2) );
\end{MapleInput}
\begin{MapleOutput}
{\it f1}\, := \,t\mapsto 1+10\, \left( 1+ \left( t-4 \right) ^{2} \right) ^{-1}
\end{MapleOutput}
データの表示
\begin{MapleInput}
> T:=[seq(f1(i),i=1..ndata)]:
  listplot(T); 
  l1:=listplot(T):
\end{MapleInput}
\MaplePlot{80mm}{./figures/C9_NonLinearFitplot2d4.eps}


ローレンツ型の関数を仮定し，関数として定義．
\begin{MapleInput}
> f:=t->a1+a2/(a3+(t-a4)^2); nparam:=4:
\end{MapleInput}
\begin{MapleOutput}
f\, := \,t\mapsto {\it a1}+{\frac {{\it a2}}{{\it a3}+ \left( t-{\it a4} \right) ^{2}}}\end{MapleOutput}
ヤコビアンの中の微分を新たな関数として定義．
\begin{MapleInput}
> for i from 1 to nparam do 
    dfda||i:=unapply(diff(f(x),a||i),x); 
  end do;
\end{MapleInput}
\begin{MapleOutputGather}
{\it dfda1}\, := \,x\mapsto 1 \notag \\
{\it dfda2}\, := \,x\mapsto  \left( {\it a3}+ \left( x-{\it a4} \right) ^{2} \right) ^{-1}
\notag \\
{\it dfda3}\, := \,x\mapsto -{\frac {{\it a2}}{ \left( {\it a3}+ \left( x-{\it a4} \right) ^{2} \right) ^{2}}} \notag \\
{\it dfda4}\, := \,x\mapsto -{\frac {{\it a2}\, \left( -2\,x+2\,{\it a4} \right) }{ \left( {\it a3}+ \left( x-{\it a4} \right) ^{2} \right) ^{2}}}  \notag
\end{MapleOutputGather}
ここで，"$||$"は連結作用素とよばれるMapleのコマンドで，$dfda||1 \mapsto dfda1$と連結する．
初期値を仮定して，データとともに関数を表示．
\begin{MapleInput}
> g1:=Vector([1,8,1,4.5]): 
  guess1:={}: 
  for i from 1 to nparam do
    guess1:={op(guess1),a||i=g1[i]}; 
  end do: 
  guess1;
\end{MapleInput}
\begin{MapleOutput}
\left\{ {\it a1}=1,{\it a2}=8,{\it a3}=1,{\it a4}= 4.5\right\}
\end{MapleOutput}

\begin{MapleInput}
> p1:=plot(subs(guess1,f(x)),x=1..ndata): 
  display(l1,p1);  
\end{MapleInput}
\MaplePlot{50mm}{./figures/C9_NonLinearFitplot2d5.eps}
見やすいように，小数点以下を3桁表示に制限する．
\begin{MapleInput}
> interface(displayprecision=3):
> df:=Vector([seq(subs(guess1,T[i]-f(i)),i=1..ndata)]);
\end{MapleInput}
\begin{MapleOutput}
{\it df}\, := \, \left[ \begin {array}{c}  0.396\\  0.897\\  2.538\\  3.600\\ - 1.400\\ - 0.462\\ - 0.103\\ - 0.016\end {array} \right]
\end{MapleOutput}

\begin{MapleInput}
> Jac:=Matrix(ndata,nparam): 
  for i from 1 to ndata do 
    for j from 1 to nparam do
      Jac[i,j]:=evalf(subs(guess1,dfda||j(i))); 
    end do: 
  end do:
  Jac;
\end{MapleInput}
\begin{MapleOutput}
\left[ \begin {array}{cccc}  1.0& 0.075&- 0.046&- 0.319\\  1.0& 0.138&- 0.152&- 0.761\\  1.0& 0.308&- 0.757&- 2.272\\  1.0& 0.800&- 5.120&- 5.120\\  1.0& 0.800&- 5.120& 5.120\\  1.0& 0.308&- 0.757& 2.272\\  1.0& 0.138&- 0.152& 0.761\\  1.0& 0.075&- 0.046& 0.319\end {array} \right]
\end{MapleOutput}

\begin{MapleInput}
> tJac:=(MatrixInverse(Transpose(Jac).Jac)).Transpose(Jac);
\end{MapleInput}
\begin{MapleOutput}
{\it tJac}\, := \, \left[ \begin {array}{cccccccc}  0.565& 0.249&- 0.354& 0.040& 0.040&- 0.354& 0.249& 0.565\\ - 2.954&- 0.506& 4.012&- 0.552&- 0.552& 4.012&- 0.506&- 2.954\\ - 0.352&- 0.029& 0.557&- 0.176&- 0.176& 0.557&- 0.029&- 0.352\\ - 0.005&- 0.012&- 0.035&- 0.080& 0.080& 0.035& 0.012& 0.005\end {array} \right] 
\end{MapleOutput}

\begin{MapleInput}
> g2:=tJac.df; 
  g1:=g1+g2;
\end{MapleInput}
\begin{MapleOutputGather}
{\it g2}\, := \, \left[ \begin {array}{c} - 0.235\\  5.592\\  0.613\\ - 0.520\end {array} \right]  \notag \\
{\it g1}\, := \, \left[ \begin {array}{c}  0.765\\  13.592\\  1.613\\  3.980\end {array} \right] \notag
\end{MapleOutputGather}
これをまたもとの近似値(guess)に入れ直して表示させると以下のようになる．カーブがデータに近づいているのが確認できよう．この操作をずれが十分小さくなるまで繰
り返す．
\begin{MapleInput}
> guess1:={seq(a||i=g1[i],i=1..nparam)};
  p1:=plot(subs(guess1,f(x)),x=1..ndata):
  display(l1,p1);
\end{MapleInput}
\begin{MapleOutput}
guess1:=\{a1=0.765, a2=13.592, a3=1.613, a4=3.980\}
\end{MapleOutput}
\MaplePlot{50mm}{./figures/C9_NonLinearFitplot2d6.eps}

4回ほど繰り返すと以下の通り，いい値に収束している．
\begin{MapleOutput}
guess1:=\{a1 = 1.006, a2 = 9.926, a3 = .989, a4 = 4.000\}
\end{MapleOutput}

